function pw () {
    # pass directory
    local prefix="${PASSWORD_STORE_DIR:-$HOME/.password-store}"

    # pass timeout
    local timeout="${PASSWORD_STORE_CLIP_TIME:-45}"

    # password var
    local pw="${@}"

    # check if exact match given
    if [[ ! -e "${prefix}/${pw}.gpg" ]]
    then
        # search for pass using completion helper
        local pass_results="$(find -L "${prefix}" -type f -not -path '*.git*' -not -name '.gpg-id' | sed -e "s#${prefix}/##" -e 's#\.gpg$##' | sort | grep -i "${pw}")"
        local no_pass_results="$(echo "${pass_results}" | grep -c .)"

        # parse results
        if [[ "${no_pass_results}" -gt 1 ]]
        then
            # print all results
            echo "${pass_results}"
            return
        elif [[ "${no_pass_results}" -eq 1 ]]
        then
            # print only result
            local pw="${pass_results}"

            echo "${0}: Only password matching search: '${pw}'"
            echo
        elif [[ "${no_pass_results}" -eq 0 ]]
        then
            # no pw match
            echo "${0}: No password match for: '${pw}'"

            return 1
        fi
    fi

    # show pass info
    DISPLAY= pass show "${pw}" | tail -n +2

    # copy or display a password
    if [[ -z "$DISPLAY" ]]
    then
        # no display, therefore showing everything
        pass show "${pw}" | head -n1
    else
        # there is a display, then use pass -c
        (pass -c "${pw}" > /dev/null) &!
    fi

    which notify-send > /dev/null 2> /dev/null && \
    {
        # notify user
        notify-send "Copied password of '${pw}' to clipboard!"

        # notify about restoring clipboard
        (sleep $timeout; notify-send 'Password cleared, clipboard restored.') &!
    }
}

# vim: ft=zsh
